apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        /**
         * To solve the bug reported at: https://goo.gl/pAZFtV
         * where buildType is not propagated properly from app module
         * down to library module.
         * The following line will publish all variants of the lib
         * and it should also be accompanied with changing
         * the compile command under dependencies{} in the app module
         * to multiple lines, one per variant. <variant>Compile
         */
        publishNonDefault true

        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        /**
         * externalNativeBuild configs that will go for every product flavor
         */
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-21', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_ARM_NEON=TRUE', '-DANDROID_STL=c++_static'
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' // these platforms cover 99% percent of all Android devices
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // release-specific configs
            externalNativeBuild {
                cmake {
                    cFlags '-O3'
                }
            }
        }

        // debug-specific configs
        debug {
            externalNativeBuild {
                cmake {
                    cFlags '-O0'
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}
